#!/usr/bin/env zsh

FG='#93a1a1'
BG='#262626'
USER_CONTENT_WIDTH=120

zmodload zsh/zselect
SCREEN_LAYOUT=($(xrandr | sed -r -n 's/.*connected ([0-9]+)x[0-9]+([-+][0-9]+).*/\2 \1/p' | sort -g))

XMONAD_LOG=
WORKSPACES=

parse_log () {
  local oifs="$IFS" fields

  IFS=$'\0'
  if ! read -t 0 -A WORKSPACES; then
    exit 0
  fi

  XMONAD_LOG=$WORKSPACES[1]
  shift WORKSPACES

  if [ "${#WORKSPACES[@]}" -eq 0 ]; then
    WORKSPACES=("0")
  fi

  IFS="$oifs"
}

refresh () {
  local oifs="$IFS" current=1 content offset width

  IFS=.
  for workspace in "${WORKSPACES[@]}"; do
    read id name <<<"$workspace"
    offset=$SCREEN_LAYOUT[$((id * 2 + 1))]
    width=$SCREEN_LAYOUT[$((id * 2 + 2))]

    if [ -n "$current" ]; then
      echo -n "^pa($offset)${XMONAD_LOG}^pa($((offset+width-USER_CONTENT_WIDTH)))^fg($FG)$USER_CONTENT^fg()"
    else
      echo -n "^pa($offset)^fg(#268bd2)^bg(#073642) ${name} ^fg()^bg()^pa($((offset+width-USER_CONTENT_WIDTH)))^fg($FG)$USER_CONTENT^fg()"
    fi

    current=
  done
  echo

  IFS="$oifs"
}

user_content() {
  date +"^ca(1,gsimplecal)%a %b %d, %H:%M^ca()"
}

typeset -i LAST_TIME=0
update_user_content () {
  local -i now=$(date +"%s")
  if ((now - LAST_TIME > 60)); then
    USER_CONTENT=$(user_content)
    LAST_TIME=now
  fi
}

update_user_content
while true; do
  refresh
  if zselect -t 6000 -A fd -r 0; then
    parse_log
  else
    update_user_content
  fi
done > >(dzen2 -fg "$FG" -bg "$BG" -ta l -e 'button3=')
